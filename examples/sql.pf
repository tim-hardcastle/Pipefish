newtype

Person = struct(name varchar(32), age int) 

cmd

init : // Create the table on initialization if it isn't there.
    put SQL -- CREATE TABLE IF NOT EXISTS People |Person|

clear :
    put SQL -- DROP TABLE People
    init

add (name string, age int) :
    put SQL --
        INSERT INTO People
        VALUES |name, age|

show (name string) :
    get personList as Person from SQL --
        SELECT * FROM People
        WHERE name=|name| 
    post prettyFmt(personList)

show all :
    get peopleList as Person from SQL --
        SELECT * FROM People
    post prettyFmt(peopleList)

def // Prettyprinting functions, omitted.



HEADING = "\n" + spaceOut("Name", "Age") + repeat(36, "-") + "\n"

prettyFmt(people list) :
    people == [] :
        "\nNo records match.\n\n"
    else :
        HEADING + (people >> spaceOut(that[name], string that[age]) -> sum) + "\n"

spaceOut(s, t) : s + repeat(33 - len(s), " ") + t + "\n"

repeat(n int, s string) : 0::n -> range >> s -> sum 

